buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'applovin-quality-service'
applovin {
    apiKey "EGmPl_ZJueWQnGa2TtGa-S5TKeqiDprTl5ylOide9qrEGHCNL_BrTn-ElBJ0NWhFt8_-4B8zrLbDX62I2gpmS5"
}
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'

repositories {
    maven { url 'https://maven.google.com' }
}

android {
    compileSdk = 34
    buildToolsVersion = '33.0.1'
    flavorDimensions = ['default']

    defaultConfig {
        applicationId "com.android.protube"
        minSdk = 23 // Android 6.0
        targetSdk = 34
        // Needs to increase the [versionName] & [versionCode] when release on Play Store
        versionName '6.0.0'
        versionCode 1

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {

        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            multiDexEnabled true
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        develop {
            resourceConfigurations += ['xxxhdpi']
        }
        production {

        }
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        encoding 'utf-8'
    }

    // speed up builds, pass Compiler Command-line Options: -PdevBuild
    if (project.hasProperty('devBuild')) {

        // disable splits
        splits.abi.enable = false
        splits.density.enable = false

        // disable PNG crunching
        aaptOptions.cruncherEnabled = false
    }

    // enable language, density, and abi configuration splits
    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }

    configurations {
        //all*.exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }

    namespace 'io.awesome.gagtube'

    tasks.withType(JavaCompile).configureEach {
        options.fork = true
        options.forkOptions.jvmArgs += [
                '--add-opens=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
                '--add-opens=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
                '--add-opens=jdk.compiler/com.sun.tools.javac.comp=ALL-UNNAMED',
                '--add-opens=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
                '--add-opens=jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED',
                '--add-opens=jdk.compiler/com.sun.tools.javac.model=ALL-UNNAMED',
                '--add-opens=jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED',
                '--add-opens=jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED',
                '--add-opens=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
                '--add-opens=jdk.compiler/com.sun.tools.javac.jvm=ALL-UNNAMED',
        ]
    }

    buildFeatures {
        viewBinding true
    }
}

ext {
    exoPlayerVersion = '2.11.8'
    roomVersion = '2.5.1'
    okHttpVersion = '4.9.0'
    icepickVersion = '3.2.0'
    markwonVersion = '4.6.2'
}

dependencies {

    // Originally in NewPipeExtractor
    implementation 'com.github.teamnewpipe:NewPipeExtractor:v0.22.7'
    implementation "com.github.TeamNewPipe:nanojson:1d9e1aea9049fc9f85e68b43ba39fe7be1c1f751"
    implementation "org.jsoup:jsoup:1.16.2"

    // androidx
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation("androidx.core:core-ktx:1.13.0")
    implementation("org.jetbrains.kotlin:kotlin-stdlib:1.9.22")

    // circle image view
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    //noinspection GradleDependency
    implementation "com.google.android.exoplayer:exoplayer:$exoPlayerVersion"
    //noinspection GradleDependency
    implementation "com.google.android.exoplayer:extension-mediasession:$exoPlayerVersion"

    // for Pair
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    
    // retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.9.0'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"

    // rxjava
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'com.artemzin.rxjava:proguard-rules:1.3.3.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    
    // rxjava2
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    
    // rxbinding2
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.2.0'
    implementation 'org.ocpsoft.prettytime:prettytime:5.0.6.Final'

    //noinspection GradleDependency
    implementation "androidx.room:room-runtime:$roomVersion"
    //noinspection GradleDependency
    implementation "androidx.room:room-rxjava2:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"

    implementation "frankiesardo:icepick:$icepickVersion"
    annotationProcessor "frankiesardo:icepick-processor:$icepickVersion"

    // google services
    implementation 'com.google.android.gms:play-services-ads:23.0.0'
    implementation 'com.google.android.gms:play-services-base:18.4.0'
    implementation 'com.google.android.gms:play-services-location:21.2.0'
    implementation "com.google.android.gms:play-services-ads-identifier:18.0.1"

    // facebook ads
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.android:audience-network-sdk:+'

    // applovin ads
    //noinspection GradleDynamicVersion
    implementation 'com.applovin:applovin-sdk:+'
    //noinspection GradleDynamicVersion
    implementation 'com.applovin.mediation:google-adapter:+'
    //noinspection GradleDynamicVersion
    implementation 'com.applovin.mediation:facebook-adapter:+'
    implementation 'com.facebook.android:facebook-android-sdk:[8,9)'

    // lifecycle
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    //noinspection LifecycleAnnotationProcessorWithJava8
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    
    // firebase
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-config:21.6.3'
    implementation 'com.google.firebase:firebase-messaging:23.4.1'

    // glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    
    // image loader
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    
    // bottom navigation
    implementation 'com.aurelhubert:ahbottomnavigation:2.3.4'

    // butterknife
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'

    // stream java8
    implementation 'com.annimon:stream:1.2.2'

    // chrome custom tabs
    //noinspection GradleDependency
    implementation 'androidx.browser:browser:1.3.0'

    // utility
    implementation 'com.darwinsys:hirondelle-date4j:1.5.1'
    implementation 'org.apache.commons:commons-text:1.10.0'
    
    // markwon
    implementation "io.noties.markwon:core:${markwonVersion}"
    implementation "io.noties.markwon:linkify:${markwonVersion}"

    // file picker
    implementation 'com.nononsenseapps:filepicker:4.2.1'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    // OneSignal
    implementation 'com.onesignal:OneSignal:[ 4.8.8]'

    // in-app updates
    implementation("com.google.android.play:app-update:2.1.0")
    implementation("com.google.android.play:app-update-ktx:2.1.0")
}
apply plugin: 'com.google.gms.google-services'
